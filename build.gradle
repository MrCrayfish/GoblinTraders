plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "${project.mod_id}-fabric"
version = "${project.mod_version}-${project.minecraft_version}"
group = project.maven_group

loom {
	runs {
		client {
			configName = "Client"
			runDir = "run"
			source = sourceSets.main
			ideConfigGenerated = true
			client()
		}

		server {
			configName = "Server"
			runDir = "run"
			source = sourceSets.main
			ideConfigGenerated = true
			server()
		}

		datagenClient {
			inherit client
			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
			vmArg "-Dfabric-api.datagen.strict-validation"
			runDir "build/datagen"
		}
	}
}

sourceSets {
	main {
		resources {
			srcDirs += [ 'src/generated/resources' ]
		}
	}
}

repositories {
	maven {
		url "file:///X:/localmaven/mcmods"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	mappings loom.officialMojangMappings()

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "com.google.code.findbugs:annotations:3.0.1"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation("com.mrcrayfish:catalogue-fabric:1.7.0-1.19.3")
	modImplementation("com.mrcrayfish:configured-fabric:2.0.2-1.19.3") {
		exclude group: 'com.terraformersmc', module: 'modmenu'
	}
}

processResources {
	inputs.property "version", project.mod_version

	filesMatching("fabric.mod.json") {
		expand "version": project.mod_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("MOD-LICENSE") {
		rename { "LICENSE_${project.mod_id}"}
	}
	from("FORGE-LICENSE") {
		rename { "LICENSE_forge"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = "${project.mod_id}-fabric"
			from components.java
		}
	}

	repositories {
		maven {
			url "file:///X:/localmaven/mcmods"
		}
	}
}
